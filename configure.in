dnl Process this file with autoconf to produce a configure script.
dnl $Id$

AC_INIT(poldek,0.17.9.2)
AC_CONFIG_SRCDIR([capreq.c])

VERSION_STATUS="devel"
VERSION_CVSTAG="v$(echo $VERSION | sed 's/\./_/g')"
AH_TEMPLATE([ENABLE_TRACE],[])
AH_TEMPLATE([HAVE_RPM_4_0_4],[])
AH_TEMPLATE([HAVE_RPM_4_1],[])
AH_TEMPLATE([HAVE_RPM_EXTDEPS],[])
AH_TEMPLATE([HAVE_TM_GMTOFF],[])
AH_TEMPLATE([HAVE_TM___GMTOFF],[])

AM_INIT_AUTOMAKE([dist-bzip2])
AC_DEFINE_UNQUOTED([VERSION_STATUS], "$VERSION_STATUS",[stable or devel version])
AC_SUBST(VERSION_CVSTAG)
#AC_CONFIG_HEADERS([config.h])
AM_CONFIG_HEADER([config.h])

ALL_LINGUAS="pl de"


AM_CFLAGS="-g -Wall -W"
AC_DEFINE([ENABLE_VFILE_RPMIO],1,[defined if rpmio is used for vfile operations])

dnl Checks for programs.
AC_PROG_CC()
AC_C_INLINE
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_CHECK_PROG(POD2MAN, pod2man, pod2man)
AM_GNU_GETTEXT([external])
AM_GNU_GETTEXT_VERSION(0.11.5)

dnl Process features.
AC_CHECK_LIB(tinfo, tigetstr,,[
	AC_CHECK_LIB(ncurses, tigetstr, ,[ 
		AC_MSG_ERROR(["tinfo nor ncurses not found"]) ]) ])

AC_CHECK_HEADERS(ncurses/curses.h,
	[ CPPFLAGS="-I/usr/include/ncurses $CPPFLAGS" ],
	[ AC_CHECK_HEADERS(curses.h,, [AC_MSG_ERROR(["curses.h not found"])],) ])

AC_ARG_ENABLE(imode,
[  --disable-imode               turn off interactive mode],
INTERACTIVE_MODE=$enableval, INTERACTIVE_MODE=yes)

if test "${INTERACTIVE_MODE}." != "yes."; then
	AM_CONDITIONAL(ENABLE_INTERACTIVE_MODE, false)
else
	AC_CHECK_LIB(readline, readline,,[ AC_MSG_ERROR(["readline not found"])])
	AC_CHECK_LIB(pcre, pcre_compile,,[ AC_MSG_ERROR(["pcre not found"])])
	AC_CHECK_HEADERS(pcre.h,,[ 
		AC_CHECK_HEADERS(pcre/pcre.h, 
			         [CPPFLAGS="-I/usr/include/pcre $CPPFLAGS"], )], )

	AM_CONDITIONAL(ENABLE_INTERACTIVE_MODE, true)
	AC_DEFINE([ENABLE_INTERACTIVE_MODE],1,[defined if interactive mode is requested])

	AC_CHECK_FUNCS(rl_completion_matches, have_rl_4_2=yes, have_rl_4_2=no,
		     $CONF_IN_LDFLAGS)
	if test "${have_rl_4_2}." = "yes."; then
		AC_DEFINE([HAVE_READLINE_4_2],1,[defined if readline 4.2 is avialiable])
	fi
fi

AC_ARG_WITH(curl,
[  --with-curl	compile with libcurl],
ENABLE_VFILE_CURL=$enableval, ENABLE_VFILE_CURL=no)
if test "${ENABLE_VFILE_CURL}." != "yes."; then
	AM_CONDITIONAL(ENABLE_VFILE_CURL, false)
else
	AC_CHECK_LIB(curl, curl_easy_init,,AC_MSG_ERROR(["libcurl not found"]))
	AM_CONDITIONAL(ENABLE_VFILE_CURL, true)
	AC_DEFINE([ENABLE_VFILE_CURL],1,[defined if curl support is requested])
fi


AC_ARG_WITH(sys-trurlib,
[  --with-sys-trurlib	compile and link with system installed trurlib],
ENABLE_SYS_TRURL=$enableval, ENABLE_SYS_TRURL=no)


AC_ARG_ENABLE(static,
[  --enable-static       build static binary],
ENABLE_STATIC=$enableval, ENABLE_STATIC=no)

if test "${ENABLE_STATIC}." = "yes."; then
	AC_DEFINE([ENABLE_STATIC],1, [defined if static linking is requested])
	poldek_LDFLAGS="-static"

	if test "${INTERACTIVE_MODE}." = "yes."; then
		LIBS="$LIBS -lncurses"
	fi

	if test "${ENABLE_VFILE_CURL}." = "yes."; then
		LIBS="$LIBS $(curl-config --libs)"
	fi
fi

AC_SUBST(poldek_LDFLAGS)


AC_ARG_ENABLE(trace,
[  --enable-trace		turn on trace messages],
ENABLE_TRACE=$enableval, ENABLE_TRACE=no)

if test "${ENABLE_TRACE}." = "yes."; then
	AC_DEFINE([ENABLE_TRACE],1,[])
fi


dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME


AC_CHECK_MEMBER(struct tm.tm_gmtoff,
	[AC_DEFINE([HAVE_TM_GMTOFF],1,[])],
	[AC_CHECK_MEMBER(struct tm.__tm_gmtoff,[ AC_DEFINE([HAVE_TM___GMTOFF],1,[])]
	,,[#include <time.h>])],
[#include <time.h>])


dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_FUNC_FNMATCH
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gettimeofday mkdir strdup uname)
AC_CHECK_FUNCS(strerror strstr strtol strtoul strsignal)
AC_CHECK_FUNCS(mallopt)
AC_CHECK_FUNCS(ngettext setlocale)

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h limits.h malloc.h locale.h)
AC_CHECK_HEADERS(sys/file.h sys/time.h syslog.h unistd.h)

AC_CHECK_HEADER(stdint.h,,AC_MSG_ERROR(["C9X compiler is needed by $PACKAGE"]))
AC_CHECK_HEADER(obstack.h,,AC_MSG_ERROR(["obstacks are needed by $PACKAGE"]))
AC_CHECK_HEADER(argp.h,,AC_MSG_ERROR(["missing required argp.h"]))

dnl RH rpm needs this
CPPFLAGS="-I/usr/include/rpm $CPPFLAGS"
AC_CHECK_HEADER(rpm/rpmlib.h,,AC_MSG_ERROR(["missing required rpmlib.h"]))

AC_MSG_CHECKING([for rpm 4.x])
#RPMDBI_PACKAGES
AC_EGREP_CPP(yes,
     [#include <rpm/rpmlib.h>
      #ifdef RPMDBI_PACKAGES
       yes
      #endif
     ], is_rpm4=yes, is_rpm4=no)

if test ${is_rpm4}. = yes. ; then
	AC_MSG_RESULT(yes)
	AC_DEFINE([HAVE_RPM_4_0],1,[defined if rpm 4.x is installed])
	AC_EGREP_CPP(RPMSENSE_SCRIPT_PRE,
		[ #include <rpm/rpmlib.h>
		], is_rpm_extdeps=yes, is_rpm_extdeps=no)
	if test ${is_rpm_extdeps}. = yes. ; then
		AC_DEFINE([HAVE_RPM_EXTDEPS])
	fi

	AC_MSG_CHECKING([for rpm >= 4.0.4])
	AC_EGREP_CPP(RPMSENSE_KEYRING,
		[ #include <rpm/rpmcli.h>
		], is_rpm4_0_4=yes, is_rpm4_0_4=no)

	if test ${is_rpm4_0_4}. = yes. ; then
		AC_DEFINE([HAVE_RPM_4_0_4],1,[])
		CFLAGS="$CFLAGS -D_REENTRANT"
		AC_MSG_RESULT(yes)
	else 
	        AC_MSG_RESULT(no)
	fi
else
	AC_MSG_RESULT(no)
fi


dnl use local ../trurlib copy if it exists
CONF_IN_LDFLAGS=

TRURL_DIR=
if test -f "${srcdir}/trurlib/n_array_new.c"; then
	TRURL_DIR="trurlib"
fi

if test "${ENABLE_SYS_TRURL}." != "no."; then
	TRURL_DIR=
fi

if test -n "$TRURL_DIR"; then
	AC_MSG_CHECKING(for local trurlib copy in $TRURL_DIR)
	AC_MSG_RESULT([yes])
	TRURL_INCLUDE="-I\$(top_srcdir)/$TRURL_DIR/include"
	TRURL_LDFLAG="-L\$(top_builddir)/$TRURL_DIR/"
	LDFLAGS="$LDFLAGS -L\$(top_builddir)/$TRURL_DIR"
	TRURLIB_VERSION=$(egrep 'T_AUTOMAKE\(trurlib, ' $srcdir/$TRURL_DIR/configure.in | cut -d ' ' -f 2 | sed 's|)||')
else
	AC_CHECK_HEADER(trurl/trurl.h,,AC_MSG_ERROR("trurl.h is missing"))
	TRURLIB_VERSION=0.43.4
fi
AC_SUBST(TRURL_INCLUDE)
AC_SUBST(TRURL_LDFLAG)
AC_SUBST(TRURLIB_VERSION)
AM_CONDITIONAL(WITH_INCLUDED_TRURLIB, test -n "$TRURL_DIR")

dnl Checks for libraries.
AC_CHECK_LIB(bz2, BZ2_bzCompress)
AC_CHECK_LIB(z, gzopen)

DBLIB=""
if test ${is_rpm4_0_4}. = no. ; then
	dnl tocheck: -ldb-3.1 causes link with static lib from /usr/lib
	dnl instead shared one from /lib
	#AC_CHECK_LIB(db-3.1, db_create, [DBLIB="-ldb-3.1"],
	#  AC_CHECK_LIB(db-3.0, db_create, [DBLIB="-ldb-3.0"],
	#    AC_CHECK_LIB(db, db_create, [DBLIB="-ldb"])
	#  )
	#)
	CPPFLAGS="-I/usr/include/db3 $CPPFLAGS"
	
	AC_CHECK_LIB(db, db_create, [DBLIB="-ldb"],,[$LIBS])
	# assume that librpm complied with --enable-db1
	if test "${ENABLE_STATIC}." = "yes."; then 
		DBLIB="$DBLIB -ldb1"
	fi
else
	AC_CHECK_LIB(db-4.0, db_create, [DBLIB="-ldb-4.0"], 
		     [AC_CHECK_LIB(db, db_create, [DBLIB="-ldb"],,[$LIBS])], 
		     [$LIBS])

	if test "${ENABLE_STATIC}." = "yes."; then 
		DBLIB="$DBLIB"
	fi

	CPPFLAGS="-I/usr/include/db4 $CPPFLAGS"
fi


AC_CHECK_LIB(popt, poptReadConfigFile)

if test ${is_rpm4_0_4}. = yes. ; then	
	AC_CHECK_LIB(beecrypt, dhaes_pContextInitEncrypt, 
	have_beecrypt=yes, have_beecrypt=no)
fi

if test ${is_rpm4}. = yes. ; then
	AC_CHECK_LIB(rpmio, rpmCleanPath,,AC_MSG_ERROR(["rpmio not found"]))
fi


AC_CHECK_LIB(rpm, rpmReadPackageInfo,[LIBS="$LIBS -lrpm $DBLIB"],
	AC_CHECK_LIB(rpm, rpmReadPackageHeader,
		[LIBS="$LIBS -lrpm -lrpmdb $DBLIB"],
		AC_CHECK_LIB(rpm, rpmReadPackageFile,
			[LIBS="$LIBS -lrpm -lrpmdb $DBLIB"
			 AC_DEFINE([HAVE_RPM_4_1],1,[])],
			AC_MSG_ERROR(["rpmlib not found"]),
			[$DBLIB -lrpmdb]),
		[$DBLIB -lrpmdb]),
	    [$DBLIB])

AC_CHECK_FUNCS(rpmMachineScore,,[ AC_MSG_ERROR(["rpmlib not found"])])
AC_CHECK_FUNCS(rpmlog rpmCheckSig rpmVerifySignature)

AC_CHECK_HEADERS(db_185.h,,[ 
	AC_MSG_WARN(["disabled synchronization with db file-dependencies"]) ])
AC_CHECK_FUNCS(__db185_open)

dnl 4.0.4 has cross dependenecies(?)
if test ${is_rpm4_0_4}. = yes. -a ${ENABLE_STATIC}. = yes. ; then
	if ${have_beecrypt}. = yes. ; then
		LIBS="$LIBS -lbz2 -lrpmio"
	fi
	LIBS="$LIBS -lbz2 -lrpmio"
fi

AC_CHECK_FUNCS(getline,, [AC_MSG_ERROR(["getline\(\) is needed by $PACKAGE"])],
	       $CONF_IN_LDFLAGS)

AC_CHECK_FUNCS(fopencookie,, [AC_MSG_WARN(["disabled gzipped files support"])],
	       $CONF_IN_LDFLAGS)

AC_CHECK_FUNCS(openpty,,
	       AC_CHECK_LIB(util, openpty, 
	[AC_DEFINE([HAVE_OPENPTY],1,[defined if openpty() is avialable])] [LIBS="$LIBS -lutil"]))

AC_CHECK_FUNCS(rpmGetRpmlibProvides,,
	[AC_MSG_WARN("[poldek will not work fine with rpmlib\(...\) capabilities"])],
	[$CONF_IN_LDFLAGS])


AC_CHECK_FUNCS(EVP_DigestInit,,
	[AC_CHECK_LIB(crypto, EVP_DigestInit,,
	 [AC_MSG_ERROR(["libcryto is needed by $PACKAGE"])]
	)], 
	[$CONF_IN_LDFLAGS])


dnl trullib 0.43.5 has n_hash_size; test if linked with system copy only
NARRAY="<trurl/narray.h>"
if test -z "$TRURL_DIR"; then
	AC_CHECK_LIB(trurl, n_hash_size,,
		[AC_MSG_ERROR(["trurlib >= 0.43.5 not found"])], [$CONF_IN_LDFLAGS])
else
	LIBS="$LIBS -ltrurl"
	AC_CONFIG_SUBDIRS(trurlib)
	NARRAY="\"$srcdir/$TRURL_DIR/include/trurl/narray.h\""
fi


AC_MSG_CHECKING([for trullib >= 0.43.9])
AC_EGREP_CPP(yes,
       [#include $NARRAY
        #ifdef n_array_has_free_fn
          yes
        #endif
       ], is_valid_trurlib_ver=yes, is_valid_trurlib_ver=no)

if test ${is_valid_trurlib_ver}. = no. ; then
	AC_MSG_RESULT(no)		
   	AC_MSG_ERROR(["trurlib >= 0.43.9 not found"])
else
	AC_MSG_RESULT(yes)	
fi

AC_CONFIG_FILES([  po/Makefile.in
		shell/Makefile
		sigint/Makefile
		vfile/Makefile
		vfile/vftp/Makefile
		vfile/vhttp/Makefile
		conf/Makefile
		Makefile
		poldek.spec
		poldek-suse.spec
		homepage.sgml
		conf/poldekrc.sample:conf/sample.src:poldekrc
		conf/poldekrc.sample-cdrom:conf/sample-cdrom.src:poldekrc
		conf/poldekrc.sample-mdk:conf/mdk.src:poldekrc
		conf/poldekrc.sample-rh:conf/rh.src:poldekrc
		conf/poldekrc.sample-pld:conf/pld.src:poldekrc
	])
AC_OUTPUT
